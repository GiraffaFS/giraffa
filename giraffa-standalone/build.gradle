project(':giraffa-standalone') {
  description = """Standalone module"""

  apply plugin: 'eclipse'

  dependencies {
    compile project(':giraffa-core')
  }

  configurations {
    giraffaWeb
  }
  dependencies {
    giraffaWeb libraries.webui
    giraffaWeb libraries.jasper
  }

  task tar(type: Tar) {
    compression = Compression.GZIP

    into("giraffa/conf") {
      from("${rootDir}/giraffa-core/src/main/resources/")
      include("*")
      includeEmptyDirs = false
    }
    into("giraffa/lib") {
      from("${rootDir}/giraffa-core/build/libs/")
      include("*.jar")
    }
    into("giraffa/bin") {
      from("${rootDir}/giraffa-standalone/src/main/resources/bin/")
      include("*")
      fileMode = 0755
    }
    into("giraffa/lib") {
      from(configurations.compile)
    }
    into("giraffa/webapps") {
      from("${rootDir}/giraffa-core/build/resources/main/webapps")
    }
    into("giraffa/lib/weblib") {
      from(configurations.giraffaWeb)
      from("${rootDir}/giraffa-core/build/libs/")
      exclude("*sources*")
    }
  }

  task('build').dependsOn tar
}